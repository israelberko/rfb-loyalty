buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.bmuschko:gradle-docker-plugin:3.5.0'
    }
}

apply plugin: com.bmuschko.gradle.docker.DockerRemoteApiPlugin

import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.DockerPushImage

docker {
    if (System.env.DOCKER_HOST) {
        url = System.env.DOCKER_HOST
    }
    if (System.env.DOCKER_CERT_PATH) {
        certPath = new File(System.env.DOCKER_CERT_PATH)
    }

    registryCredentials {
        //url default value
        //url = 'https://index.docker.io/v1/'
        username = 'israelber'
        password = (System.env.DOCKER_HUB_PWD)
        email = 'isbe90@gmail.com'
    }
}

task copyDockerFiles(type: Copy) {
    description = "Copy Dockerfile and required data to build directory"
    from 'src/main/docker'
    from "${project.buildDir}/libs"
    into { "${project.buildDir}/docker" }
    include "*"
    exclude "**/*.yml"
}

def rfbImageName = 'israelber/rfbloyalty'

task buildDocker(type: DockerBuildImage, dependsOn: 'copyDockerFiles') {
    description = "Package application as Docker image"
    group = "Docker"
    inputDir = project.file("${project.buildDir}/docker")
    tags = ["rfbloyalty:latest".toString(), "rfbloyalty:${project.version}".toString()]
}


task pushDockerLatest(type: DockerPushImage, dependsOn: 'buildDocker') {
    imageName = rfbImageName
    tag = "latest"
}

/**
 * Use to push project version tag to docker hub
 */
task pushDockerVersionTag(type: DockerPushImage, dependsOn: 'buildDocker') {
    imageName = rfbImageName
    tag =  "${project.version}".toString()
}
